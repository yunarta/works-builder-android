buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://dl.bintray.com/yunarta-kartawahyudi/maven'
        }
        maven {
            url 'http://dl.bintray.com/yunarta-kartawahyudi/snapshot'
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.0.0-alpha2"
        classpath 'com.mobilesolutionworks:works-bintray-publish:1.0.0'
        classpath 'com.mobilesolutionworks:works-jenkins-android-builder:1.0.0'
        classpath 'com.mobilesolutionworks:works-autoconfig:1.0.0'
    }
}

Properties prop = new Properties()
def File local = [project.rootDir, 'local.properties']

local.withInputStream { is ->
    prop.load(is)
}

repositories {
    jcenter()
    maven { url prop['sdk.dir'] + '/extras/android/m2repository' }
    maven {
        url 'http://repository.grepcode.com/java/ext'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.mobilesolutionworks.android-autoconfig'
apply plugin: 'com.mobilesolutionworks.jenkins-configure'
apply plugin: 'com.mobilesolutionworks.bintray-publish'

group 'com.mobilesolutionworks'
version jenkins.version('1.1.0')

apply from: ('https://raw.githubusercontent.com/yunarta/works-gradle-script/master/auto-configure.gradle')

sourceCompatibility = 1.7
targetCompatibility = 1.7

android {
    resourcePrefix 'works_'

    compileSdkVersion autoConfig.sdk(23)
    buildToolsVersion autoConfig.buildTools('23.0.2')

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion autoConfig.sdk(23)

        versionCode jenkins.build(1)
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    provided 'com.google.android:android:4.4_r1'
    provided 'com.android.support:support-annotations:' + autoConfig.support('23.1.1')
}

worksPublish {
    bintray {
        repo = 'maven'
        name = group + ':' + 'works-builder'
    }

    developer {
        id = 'yunarta'
        name = 'Yunarta Kartawahyudi'
        email = 'yunarta.kartawahyudi@gmail.com'
    }

    siteUrl = 'https://github.com/yunarta/works-builder-android'
    gitUrl = 'https://github.com/yunarta/works-builder-android.git'
}